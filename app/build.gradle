apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86":3, "x86_64":4]
ext.universalBase = 8000

android {
    namespace 'is.xyz.mpv'
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 33
        versionName "2023-11-30-release"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "default"
    productFlavors {
        "default" {
            isDefault true
        }
        api29 {
            targetSdkVersion 29
            versionNameSuffix "-oldapi"
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    if (isDebug()) {
        packagingOptions {
            doNotStrip "*/armeabi/*.so"
            doNotStrip "*/armeabi-v7a/*.so"
            doNotStrip "*/arm64-v8a/*.so"
            doNotStrip "*/x86/*.so"
            doNotStrip "*/x86_64/*.so"
            doNotStrip "*/mips/*.so"
            doNotStrip "*/mips64/*.so"
            //...
        }
    }
    splits {
        abi {
            enable true
            reset()
            // include only the ABIs that were actually built
            project.ext.abiCodes.each { abi, _ ->
                if (new File(project.projectDir, "src\\main\\jniLibs\\${abi}").exists())
                    include(abi)
            }
            universalApk true // build an APK with all ABIs too
        }
    }

    // https://youtrack.jetbrains.com/issue/KT-55947
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

// Map versionCode so each ABI gets a different one
// e.g. x86 with version 21 gets a versionCode of 3021
import com.android.build.VariantOutput
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def base = project.ext.abiCodes.get(output.getFilter(VariantOutput.ABI))
        // universal APK just gets a constant added to it
        if (base != null)
            output.versionCodeOverride = base * 1000 + variant.versionCode
        else
            output.versionCodeOverride = universalBase + variant.versionCode
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.media:media:1.7.0'
}

boolean isDebug() {
    boolean ret = false
    try {
        Properties properties = new Properties()
        File file = project.rootProject.file('local.properties')
        if (!file.exists()) {
            return false
        }
        properties.load(file.newDataInputStream())
        String debugStr = properties.getProperty("debug")
        if (debugStr != null && debugStr.length() > 0) {
            ret = debugStr.toBoolean()
        }
    } catch (Throwable throwable) {
        throwable.printStackTrace()
        ret = false
    }
    project.logger.error("[${project.name}]Debug:${ret}")
    return ret
}